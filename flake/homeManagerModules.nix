{ getSystem, ... }:
{
  flake = {
    homeManagerModules.default =
      {
        config,
        lib,
        pkgs,
        ...
      }:
      with lib;
      let
        cfg = config.programs.neovim-pde;
      in
      {
        options = {
          programs.neovim-pde = {
            enable = mkEnableOption "Neovim PDE";

            extendGitIgnores = mkOption {
              type = types.bool;
              default = true;
              description = ''
                Whether to add misc files generated by neovim, lsp, dap etc. to default global git ignore.
              '';
            };

            notesPath = mkOption {
              type = types.nullOr types.path;
              default = null;
              description = ''
                Absolute path to root folder for quick-notes functionality.
                Provide null to keep this in XDG state folder.
              '';
            };

            spellPath = mkOption {
              type = types.nullOr types.path;
              default = null;
              description = ''
                Absolute path to root folder of spellfiles.
                Provide null to keep it in XDG state folder.
              '';
            };

            devConfigurationPath = mkOption {
              type = types.nullOr types.path;
              default = null;
              description = ''
                Optional absolute path to root folder of neovim-pde configuration (e.g. root folder in this
                repository).
                Must contain `nvim` folder inside.
                If provided, `nvim-dev` package will be available globally across your system.
                It allows to start neovim-pde in dev mode, which is just dynamically reading lua files from the
                directory you provide.
                This means no system rebuilds necessary to change your config.
                Provide null to disable this functionality.
              '';
            };
          };
        };

        config =
          let
            nvim = (getSystem pkgs.system).packages.nvim;
            nvim-dev = pkgs.writeShellScriptBin "nvim-dev" ''
              export NVIM_PDE_DEV_CONFIG_PATH="${cfg.devConfigurationPath}"
              ${lib.getExe (getSystem pkgs.system).packages.nvim-dev};
            '';
          in
          mkIf cfg.enable {
            home.packages = [ nvim ] ++ (optionals (cfg.devConfigurationPath != null) [ nvim-dev ]);

            home.sessionVariables = {
              # should be like that but many programs don't respect VISUAL in favor of EDITOR so...
              # EDITOR = "nvim -u NONE -e";
              EDITOR = lib.mkDefault (lib.getExe nvim);
              VISUAL = lib.mkDefault (lib.getExe nvim);
              GIT_EDITOR = lib.mkDefault "${lib.getExe nvim} --clean";
              MANPAGER = lib.mkDefault "${lib.getExe nvim} --clean +Man!";
            };

            xdg.stateFile."nvim/notes" = {
              enable = cfg.notesPath != null;
              source = cfg.notesPath;
            };

            xdg.stateFile."nvim/spell" = {
              enable = cfg.spellPath != null;
              source = cfg.spellPath;
            };

            programs.git.ignores = mkIf cfg.extendGitIgnores [
              ".netcoredbg_hist"
              ".nvim.lua"
            ];

            programs.bash.shellAliases = lib.mkDefault { vimdiff = "nvim -d"; };
            programs.fish.shellAliases = lib.mkDefault { vimdiff = "nvim -d"; };
            programs.zsh.shellAliases = lib.mkDefault { vimdiff = "nvim -d"; };
          };
      };
  };
}
